# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: spider-mario <spidermario@free.fr>
# Modified by: Kuan-Yen Chou <forendef2846@gmail.com>

pkgname=afl
pkgver=2.39b
pkgrel=1
pkgdesc='Security-oriented fuzzer using compile-time instrumentation and genetic algorithms'
url='http://lcamtuf.coredump.cx/afl/'
arch=('i686' 'x86_64')
license=('Apache')
depends=('glibc' 'bash')
optdepends=('gcc: gcc instrumentation support'
            'gnuplot: graph plotting support')
makedepends=('gcc' 'wget' 'python' 'python2' 'libtool' 'automake' 'autoconf' 'coreutils' 'bison')
provides=('american-fuzzy-lop')
replaces=('american-fuzzy-lop')
options=('!emptydirs' '!strip')
source=(${pkgname}-${pkgver}.tgz::http://lcamtuf.coredump.cx/${pkgname}/releases/${pkgname}-${pkgver}.tgz
        qemu-2.8.0.diff
        ${pkgname}-${pkgver}.diff)
sha512sums=('0df37fc8dea65d4130bc0fc8aaa23525bde4832c7202efb78ebe740d661546adee575b48fb94d851ef26293ca28a6ff818ab1dfd643c8e688aceb520e11229d0'
            '2ff7222ef250e4804f466f95952b6e5951c5e801d37fe9a126c00254f1837e199d9c30e5610ef5fb7b379a1e354b812a656d76d51b0f7a182960c5fafbfe7ac9'
            '1dfb8fea09ea8273e9bd07c80942626ff5e72974034b6b5bfe3bc2e46d30db37e9322afff629d08fa0d55fbf0a9fb26e9e00eb14adf6749c10219906b92e0c53')

prepare() {
  rm -f $(readlink ${pkgname}-${pkgver}.tgz)
  mv ${srcdir}/qemu-2.8.0.diff ${srcdir}/${pkgname}-${pkgver}/qemu_mode/patches
  patch -p0 < ${pkgname}-${pkgver}.diff
}

build() {
  cd ${pkgname}-${pkgver}
  export CC=gcc
  export CXX=g++
  export AFL_NO_X86=1
  make PREFIX=/usr
  cd qemu_mode
  ./build_qemu_support.sh
  cd ..
}

package() {
  cd ${pkgname}-${pkgver}
  AFL_NO_X86=1 make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 llvm_mode/README.llvm -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
